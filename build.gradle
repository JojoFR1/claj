import com.xpdustry.toxopid.spec.ModMetadata
import com.xpdustry.toxopid.spec.ModPlatform
import java.nio.charset.StandardCharsets
import java.nio.file.Files
import java.util.regex.Pattern

plugins {
  id "java"
  id "com.xpdustry.toxopid" version "4.1.2"
  id "net.kyori.indra.publishing" version "3.1.3"
}

def metadata = ModMetadata.fromJson(file("mod.hjson"))
group = "com.xpdustry"
version = metadata.version
description = metadata.description

allprojects {
  apply plugin: "java"
  apply plugin: "com.xpdustry.toxopid"
  
  sourceSets.main.java.srcDirs = ["src"]
  sourceSets.main.resources.srcDirs = ["resources"]
  toxopid.compileVersion = "v${metadata.minGameVersion}"
  ext.arcNet = "com.github.Anuken.Arc:arcnet:${toxopid.compileVersion}" //toxopid.dependencies.arcnet
  
  repositories {
    mavenCentral()
    maven { url "https://maven.xpdustry.com/mindustry" }
    maven { url "https://www.jitpack.io" }
  }

  // Force java 1.8
  compileJava {
    targetCompatibility = JavaVersion.VERSION_1_8
    sourceCompatibility = JavaVersion.VERSION_17
    options.encoding = "UTF-8"
    options.release = 8
    
    // Needed due to Anuken's modified jabel
    options.fork = true
    options.forkOptions.jvmArgs += [
      "--add-opens=jdk.compiler/com.sun.tools.javac.api=ALL-UNNAMED",
      "--add-opens=jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED",
      "--add-opens=jdk.compiler/com.sun.tools.javac.model=ALL-UNNAMED",
      "--add-opens=jdk.compiler/com.sun.tools.javac.processing=ALL-UNNAMED",
      "--add-opens=jdk.compiler/com.sun.tools.javac.parser=ALL-UNNAMED",
      "--add-opens=jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED",
      "--add-opens=jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED",
      "--add-opens=java.base/sun.reflect.annotation=ALL-UNNAMED"
    ]
  }
  
  dependencies {
    // Temporary: added record desugarization. See: https://github.com/xpdustry/jabel
    annotationProcessor rootProject.files("libs/jabel.jar") //"com.github.Anuken:jabel:0.9.0"
  }
}

project(":common") {
  dependencies {
    compileOnly toxopid.dependencies.arcCore
    compileOnly arcNet
  }
}

project(":api") {
  dependencies {
    implementation project(":common")
    compileOnly toxopid.dependencies.arcCore
    compileOnly arcNet
  }
}

project(":client") {
  toxopid.platforms = [ModPlatform.DESKTOP, ModPlatform.ANDROID]

  dependencies {
    implementation project(":common")
    implementation project(":api")
    compileOnly toxopid.dependencies.arcCore
    compileOnly toxopid.dependencies.mindustryCore
  }
  
  jar {
    from {
      configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
  }

  mergeJar {
    archiveFileName = "${metadata.name}-${project.name}.jar"

    // Includes mod json and icon
    from(rootDir) {
      include "mod.hjson"
      include "icon.png"
    }

    // Copy the builded jar to the working directory
    doLast {
      copy {
        from mergeJar
        into rootDir
      }
    }
  }
	
  // Includes android dex
  build.dependsOn mergeJar

  // Make compatible with Mindustry v8. Will just replace 'minGameVersion' in mod.hjson.
  task buildV8(type: Jar, dependsOn: [mergeJar]) {
    def modHjsonText = rootProject.file("mod.hjson").getText("UTF-8")
    def mv8Match = (modHjsonText =~ /(?m)^\s*minGameVersionV8\s*:\s*"([^"]+)"/)
    def v8MinGameVersion = mv8Match.find() ? mv8Match.group(1) : null
    if (v8MinGameVersion == null) throw new Exception("mod.hjson is missing 'minGameVersionV8'.")

    archiveFileName = "${metadata.name}-${project.name}-v8.jar"

    // Start from the jar produced by mergeJar, but drop its mod.hjson entry.
    from({ zipTree(mergeJar.archiveFile.get().asFile) }, { exclude "mod.hjson" })
    
    // Add a filtered mod.hjson at the root of the jar.
    from(rootDir) {
      include "mod.hjson"
      //includeEmptyDirs = false
      filter {
        if (it == null) return null
        if (it ==~ /^\s*minGameVersionV8\s*:.*$/) return null
        if (it ==~ /^\s*minGameVersion\s*:.*$/) return "minGameVersion: \"${v8MinGameVersion}\""
        return it
      }
    }

    doLast {
      copy {
        from buildV8
        into rootDir
      }
    }
  }
}

project(":server") {
  ext.mainClassName = "com.xpdustry.claj.server.Main"
  toxopid.platforms = [ModPlatform.SERVER]
  toxopid.compileVersion = "v154"

  dependencies {
    implementation project(":common")
    implementation toxopid.dependencies.arcCore
    implementation arcNet
    implementation toxopid.dependencies.arcHeadless
  }
  
  jar {
    archiveFileName = "${metadata.name}-${project.name}.jar"
    manifest.attributes["Main-Class"] = project.mainClassName
    manifest.attributes["Claj-Version"] = metadata.version
    
    // Includes arc libs
    from {
      configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    
    // Copy the builded jar to the working directory
    doLast {
      copy {
        from jar
        into rootDir
      }
    }
    
    //exclude "arc/assets/**", "arc/audio/**", "arc/input/**", "arc/scene/**", "arc/util/noise/**", "arc/util/viewport/**"
  }
  
  task run(dependsOn: classes, type: JavaExec) {
    mainClass = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = buildDir
    args 7000 // default port
    jvmArgs "-DClaj-Version=${metadata.version}" // claj version
  }
}

build.dependsOn "client:buildV8"
task buildAll(type: Jar, dependsOn: ["client:build", "client:buildV8", "server:build"]) {}


// Publishing
signing {
  def signingKey = findProperty("signingKey")
  def signingPassword = findProperty("signingPassword")
  useInMemoryPgpKeys(signingKey, signingPassword)
}

indra {
  javaVersions {
    target(8)
    minimumToolchain(8)
  }
    
  gpl3OnlyLicense()
  publishReleasesTo("xpdustry", "https://maven.xpdustry.com/releases")

  def repo = metadata.repository.split("/")
  github(repo[0], repo[1]) {
    ci(true)
    issues(true)
    scm(true)
  }

  configurePublications {
    from components.java
    artifact sourcesJar
    artifact javadocJar
  
    pom {
      organization {
        name = "Xpdustry"
        url = "https://www.xpdustry.com"
      }

      developers {
        metadata.author.split(",").each { author ->
          developer {
            id = author.trim()
            url = "https://github.com/${author.trim()}"
          }
        }
      }
    }
  }
}

// I want these to only be included when publishing, not all the time...
task javadocJar(type: Jar, dependsOn: 'javadoc') {
  from javadoc.destinationDir
  archiveClassifier = 'javadoc'
}
task sourcesJar(type: Jar, dependsOn: 'classes') {
  from sourceSets.main.allSource
  archiveClassifier = 'sources'
}